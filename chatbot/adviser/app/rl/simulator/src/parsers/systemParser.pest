rules = _{ SOI ~ (text | "{{" ~ expr ~ "}}")*  ~ EOI }

expr = _{ addition }
addition = { (multiplication ~ (add | subtract))* ~ multiplication }
multiplication = { (term ~ (multiply | divide))* ~ term }
term = _{ "(" ~ expr ~ ")" | negative_number | number | default | constant | trueval | falseval | function | identifier }

function = { identifier ~ "." ~ identifier ~ "(" ~ fn_args? ~ ")"}
fn_args = { (expr ~ ",")* ~ expr }

add      = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }

eq = { "==" }
neq = { "!=" }
le = { "<=" }
lt = { "<" }
gt = { ">" }
ge = { ">=" }
comperator = _{ eq | neq | le | lt | ge | gt }

char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
inner = @{ char* }
constant = ${ "\"" ~ inner ~ "\"" }
identifier = @{ !(and | or | trueval | falseval) ~ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }
default = { "DEFAULT" }

number = @{ ASCII_DIGIT+ }
negative_number = { "-" ~ number }

trueval = { "TRUE" }
falseval = { "FALSE" }
and = _{ "AND" }
or = _{ "OR" }

text_inner = { !("{{" | "}}" | WHITESPACE) ~ ANY  }
text = @{ text_inner+ }

WHITESPACE = _{ " " | "\t" }